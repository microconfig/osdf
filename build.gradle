plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
    id "io.freefair.lombok" version "5.0.0"
    id 'org.sonarqube' version '2.8'
}

group 'io.microconfig.osdf'
version '1.3.6'

jar {
    manifest {
        attributes(
                'Main-Class': 'io.microconfig.osdf.CLIStarter',
                'Multi-Release': true
        )
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        nexus(MavenPublication) {
            pom {
                name = 'OSDF'
                description = 'OpenShift deploy tool based on Microconfig'
                url = 'https://github.com/microconfig/osdf'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/microconfig/osdf.git'
                    developerConnection = 'scm:git:https://github.com/microconfig/osdf.git'
                    url = 'https://github.com/microconfig/osdf'
                }
                developers {
                    developer {
                        id = 'amatorin'
                        name = 'Alexander Matorin'
                        email = 'alx.matorin@gmail.com'
                    }
                    developer {
                        id = 'apavlov'
                        name = 'Artem Pavlov'
                        email = 'admpavlov@yandex.ru'
                    }
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.nexus
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        html.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "microconfig_osdf"
    }
}

apply from: 'jmeter.gradle'

dependencies {

    implementation 'org.yaml:snakeyaml:1.25'
    implementation 'commons-io:commons-io:2.5'
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'commons-codec:commons-codec:1.14'
    implementation 'io.microconfig:microconfig-cli:4.0.0:all'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testImplementation 'org.mockito:mockito-core:2.1.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'

}

test {
    useJUnitPlatform()
}